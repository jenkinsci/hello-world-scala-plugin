import java.util.regex.Matcher

plugins {
    id "org.jenkins-ci.jpi" version "0.39.0"
}

group = "org.jenkins-ci.plugins"
version = "0.0.1"    
description = "A plugin written in scala"

apply plugin: 'scala'

tasks.withType(ScalaCompile) {
    // Use incremental compilation
    //scalaCompileOptions.useAnt = false
    // Enable Scala warnings output
    scalaCompileOptions.additionalParameters = ["-Yresolve-term-conflict:object"]
    targetCompatibility = 1.8
}

sourceSets.main.scala.srcDir "src/main/java"
sourceSets.main.java.srcDirs = []

jenkinsPlugin {
    coreVersion = "2.257"                                               // Version of Jenkins core this plugin depends on.
    displayName = "Hello World plugin built with Scala"                 // Human-readable name of plugin.
    url = "http://wiki.jenkins-ci.org/display/JENKINS/scala-hello-world-plugin"   // URL for plugin on Jenkins wiki or elsewhere.
    gitHubUrl = "https://wiki.jenkins-ci.org/display/JENKINS/Scala+Hello+World+Plugin" // Plugin URL on GitHub. Optional.
    shortName = "scala-hello-world"                                   // Plugin ID, defaults to the project name without trailing '-plugin'

    // The developers section is optional, and corresponds to the POM developers section.
    developers {
        developer {
            id "jeremymarshall"
            name "Jeremy Marshall"
            email "jeremystuartmarshall@gmail.com"
        }
    }

    ext {
        versions = [
                scala     : '2.13.3',
                scalatest : '3.2.2'
        ]
    }

    dependencies {
        implementation "org.jenkins-ci.plugins.workflow:workflow-step-api:2.22@jar"
        implementation "org.jenkins-ci.plugins:matrix-project:1.16@jar"
        implementation group: 'org.scala-lang', name: 'scala-library', version: "$versions.scala"
        implementation group: 'org.scala-lang', name: 'scala-compiler', version: "$versions.scala"
        testImplementation group: 'org.scalatest', name: 'scalatest_2.13', version: "$versions.scalatest"
    }

}

task annotate(dependsOn:compileScala, type: Exec)  {

    if (logging.level >= LogLevel.INFO) {
        args '-verbose'
    }

    doFirst{
        args '-proc:only'
        args '-d'
        args compileJava.destinationDir
        args '-classpath'
        args compileJava.classpath.getAsPath() + java.io.File.pathSeparatorChar + compileJava.destinationDir

        if (logging.level >= LogLevel.INFO) {
            args '-verbose'
        }

        fileTree(compileScala.destinationDir).each { File file ->
            if(file.path.contains('META-INF')) {
                return
            }
            if(!file.path.endsWith('class')) {
                return
            }
            args file.path.minus(compileScala.destinationDir).minus('.class').substring(1).replaceAll(Matcher.quoteReplacement(File.separator),'.')
        }
    }

    executable "javac"
}

compileScala.doLast {annotate.execute()}
